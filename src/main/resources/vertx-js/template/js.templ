@comment{"Generate a call to a Java API method"}
@comment{"===================================="}

@declare{'genMethodCall'}
@if{static}J@{ifaceSimpleName}@else{}j_@{ifaceName}@end{}.@{method.name}(
	@code{pcnt=0;}
	@foreach{param: method.params}
		@code{argName='__args[' + (pcnt++) + ']';}
		@includeNamed{'convParam'}
	@end{", "})
@end{}

@comment{"Generate the code that converts a parameter from JavaScript to Java to call a Java API method"}
@comment{"============================================================================================="}

@declare{'convParam'}
	@code{paramName = overloaded ? argName : param.name;}
	@if{param.type.name.startsWith('io.vertx.core.Handler<')}
		@code{genericType = helper.getGenericType(param.type.name)}
		@if{genericType.startsWith('io.vertx.core.AsyncResult<')}
			@code{genericType = helper.getGenericType(genericType)}
			@if{genericType.equals('java.lang.Void')}
function(ar) {\n
@{ind}    if (ar.succeeded()) {\n
@{ind}      @{paramName}(null, null);\n
@{ind}    } else {\n
@{ind}      @{paramName}(null, ar.cause());\n
@{ind}    }\n
@{ind}  }
			@else{}
function(ar) {\n
@{ind}    if (ar.succeeded()) {\n
@{ind}      @{paramName}(@includeNamed{'convReturn'; templ='arVal'; returnType=genericType}, null);\n
@{ind}    } else {\n
@{ind}      @{paramName}(null, ar.cause());\n
@{ind}    }\n
@{ind}  }
			@end{}
		@else{genericType.equals('java.lang.Void')}
@{paramName}
		@else{}
function(jVal) {\n
@{ind}    @{paramName}(@includeNamed{'convReturn'; templ='basicVal'; returnType=genericType});\n
@{ind}  }
		@end{}
	@else{param.type.name.equals('io.vertx.core.json.JsonObject')}
utils.convJSObjectToJsonObject(@{paramName})
	@else{param.type.name.equals('io.vertx.core.json.JsonArray')}
utils.convJSArrayToJsonArray(@{paramName})
	@else{param.options}
@{paramName} != null ? @{helper.getSimpleName(param.type.name)}.optionsFromJson(new JsonObject(JSON.stringify(@{paramName}))) : null
	@else{param.type.kind == CLASS_OBJECT}
utils.convRuntimeParam(@{paramName})
	@else{helper.isBasicType(param.type.name)}
@{paramName}
	@else{}
@{paramName}._jdel()
	@end{}
@end{}

@declare{'basicVal'}
jVal
@end{}

@declare{'arVal'}
ar.result()
@end{}

@comment{"Generate the code that converts a Java return to the corresponding JavaScript value"}
@comment{"This is also used for converting values returned from Java API via handlers"}
@comment{"==================================================================================="}

@declare{'convReturn'}
	@code{genericType=helper.getGenericType(returnType)}
	@if{returnType.startsWith('java.util.List')}
		@if{genericType.equals('io.vertx.core.json.JsonObject') || genericType.equals('io.vertx.core.json.JsonArray')}
utils.convListSetJson(@includeNamed{templ})
		@else{helper.isVertxGenType(helper.getGenericType(returnType))}
utils.convListSetVertxGen(@includeNamed{templ}, @{helper.getSimpleName(helper.getGenericType(returnType))})
		@else{}
@includeNamed{templ}
		@end{}
	@else{returnType.startsWith('java.util.Set')}
		@if{genericType.equals('io.vertx.core.json.JsonObject') || genericType.equals('io.vertx.core.json.JsonArray')}
utils.convListSetJson(@includeNamed{templ})
		@else{helper.isVertxGenType(helper.getGenericType(returnType))}
utils.convListSetVertxGen(@includeNamed{templ}, @{helper.getSimpleName(helper.getGenericType(returnType))})
		@else{}
utils.convSet(@includeNamed{templ})
		@end{}
	@else{returnType.equals('io.vertx.core.json.JsonObject') || returnType.equals('io.vertx.core.json.JsonArray')}
utils.convJsonToJS(@includeNamed{templ})
	@else{helper.isBasicType(returnType)}
@includeNamed{templ}
	@else{method.returnType.kind == CLASS_API}
new @{helper.getSimpleName(helper.getNonGenericType(returnType))}(@includeNamed{templ})
	@else{}
	@comment{'This will probably happen if the return type is generic'}
utils.convRuntimeReturn(@includeNamed{templ})
	@end{}
@end{}

@comment{"Generate a JavaScript Method"}
@comment{"============================"}

@declare{'genMethod'}
	@code{methodList = methodsByName.get(methodName); overloaded = methodList.size() > 1; method = methodList.get(0);}
	@if{static == method.staticMethod}
		@if{method.comment != null}
@{helper.indentString("/*\n" + helper.removeTags(method.comment) + "\n*/\n", ind)}
		@end{}
@{ind}@if{static}@{ifaceSimpleName}@else{}this@end{}.@{methodName} =
		@if{overloaded}
		 function() {\n
		@else{}
		 function(@foreach{param: method.params}@{param.name}@end{", "}) {\n
		@end{}
			@code{mcnt = 0;}
@{ind}  var __args = arguments;\n
			@foreach{method: methodList}
@{ind}@if{mcnt == 0}  if@else{}else if@end{} (__args.length === @{method.params.size()}
				@code{mcnt++;}
				@code{cnt = 0;}
				@if{method.params.size() > 0} && @end{}
				@foreach{param: method.params}
					@if{param.type.kind == CLASS_PRIMITIVE || param.type.kind == CLASS_BOXED_PRIMITIVE}
					typeof __args[@{cnt++}] ===
						@if{param.type.name == 'long' || param.type.name == 'java.lang.Long' ||
								param.type.name == 'int'  || param.type.name == 'java.lang.Integer' ||
								param.type.name == 'short' || param.type.name == 'java.lang.Short' ||
								param.type.name == 'float' || param.type.name == 'java.lang.Float' ||
								param.type.name == 'double' || param.type.name == 'java.lang.Double' ||
								param.type.name == 'byte' || param.type.name == 'java.lang.Byte'}
							'number'
						@else{param.type.name == 'boolean' || param.type.name == 'java.lang.Boolean'}
							'boolean'
						@else{param.type.name == 'char' || param.type.name == 'java.lang.Character'}
							'string'
						@end{}
					@else{param.type.kind == CLASS_STRING}
					typeof __args[@{cnt++}] === 'string'
					@else{param.type.kind == CLASS_API}
						typeof __args[@{cnt}] === 'object' && __args[@{cnt++}]._vertxgen
					@else{param.type.kind == CLASS_JSON_ARRAY}
						typeof __args[@{cnt}] === 'object' && __args[@{cnt++}] instanceof Array
					@else{param.type.kind == CLASS_HANDLER}
						typeof __args[@{cnt++}] === 'function'
					@else{param.type.kind == CLASS_OBJECT}
						true
					@else{}
						typeof __args[@{cnt++}] === 'object'
					@end{}
				@end{' && '}) {\n
				@if{!method.returnType.name.equals("void")}
					@if{method.fluent}
@{ind}    @includeNamed{'genMethodCall'};\n
						@if{static}
@{ind}    return @{ifaceSimpleName};\n
						@else{}
@{ind}    return that;\n
						@end{}
					@else{method.cacheReturn}
@{ind}    if (that.cached@{method.name} == null) {\n
@{ind}      that.cached@{method.name} = @includeNamed{'convReturn'; templ='genMethodCall'; returnType=method.returnType.name};\n
@{ind}    }\n
@{ind}    return that.cached@{method.name};\n
					@else{}
@{ind}    return @includeNamed{'convReturn'; templ='genMethodCall'; returnType=method.returnType.name};\n
					@end{}
				@else{}
@{ind}    @includeNamed{'genMethodCall'};\n
				@end{}
@{ind}  }
			@end{} else utils.invalidArgs();\n
@{ind}};\n\n
	@end{}
@end{}

@comment{"This is where the generation starts"}
@comment{"==================================="}

/*\n
 * Copyright 2014 Red Hat, Inc.\n
 *\n
 * Red Hat licenses this file to you under the Apache License, version 2.0\n
 * (the "License"); you may not use this file except in compliance with the\n
 * License.  You may obtain a copy of the License at:\n
 *\n
 * http://www.apache.org/licenses/LICENSE-2.0\n
 *\n
 * Unless required by applicable law or agreed to in writing, software\n
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT\n
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.  See the\n
 * License for the specific language governing permissions and limitations\n
 * under the License.\n
 */\n\n

@comment{"Generate the requires"}

var utils = require('vertx-js/util/utils');\n
@foreach{referencedType : referencedTypes}
@code{refedType=referencedType.simpleName;}
var @{refedType} = require('@{referencedType.moduleName}-js/@{helper.convertCamelCaseToUnderscores(refedType)}');\n
@end{}
\n

@comment{"The top level vars for the module"}

var io = Packages.io;\n
var JsonObject = io.vertx.core.json.JsonObject;\n
var J@{ifaceSimpleName} = @{helper.getNonGenericType(ifaceFQCN)};\n
@foreach{optionType: referencedOptionsTypes}
var @{helper.getSimpleName(optionType)} = @{optionType};\n
@end{}
\n
@code{ifaceName = helper.decapitaliseFirstLetter(ifaceSimpleName)}

@comment{"The main comment"}

/**\n
@if{ifaceComment != null}
@{helper.removeTags(ifaceComment)}\n
@end{}
  @class\n
*/\n

@comment{"The constructor"}

var @{ifaceSimpleName} = function(j_val) {\n\n
  var j_@{ifaceName} = j_val;\n
  var that = this;\n

@comment{"Apply any supertypes"}

@foreach{superType: superTypes}
  @{helper.getSimpleName(helper.getNonGenericType(superType))}.call(this, j_val);\n
@end{}
\n

@comment{"Now iterate through each unique method"}

@foreach{methodName:methodsByName.keySet()}

@comment{"Call out to actually generate the method, we only consider non static methods here"}
@includeNamed{'genMethod';static=false;ind="  "}

@end{}

@comment{"Each object has a _jdel function which gives access to the underlying Java object"}

  this._vertxgen = true;\n\n

  // Get a reference to the underlying Java delegate\n
  this._jdel = function() {\n
    return j_@{ifaceName};\n
  }\n\n
};\n
\n

@comment{"Iterate through the methods again, this time only considering the static ones"}

@foreach{methodName:methodsByName.keySet()}

@comment{"Call out to generate the static method"}
@includeNamed{'genMethod';static=true;ind=""}

@end{}

// We export the Constructor function\n
module.exports = @{ifaceSimpleName};





